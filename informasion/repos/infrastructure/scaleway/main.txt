provider "scaleway" {
  zone   = "fr-par-1"
  region = "fr-par"
}

provider "onepassword" {}

provider "helm" {
  kubernetes {
    host  = null_resource.kubeconfig.triggers.host
    token = null_resource.kubeconfig.triggers.token
    cluster_ca_certificate = base64decode(
      null_resource.kubeconfig.triggers.cluster_ca_certificate
    )
  }
}

provider "kubernetes" {
  host  = null_resource.kubeconfig.triggers.host
  token = null_resource.kubeconfig.triggers.token
  cluster_ca_certificate = base64decode(
    null_resource.kubeconfig.triggers.cluster_ca_certificate
  )
}

##########
# Locals #
##########

locals {
  ssh_keys = [
    "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIC90w9KIPh9pCh53Ys6D55li+7Rho6GpTC/kHiktGwMa hi@mogaal.com",
    "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABgQCtmSIppOj8V3XegpNuwTyh3EZO27vFrFoR8ZUfyYKuFf1MSwFXNfUqdFyCehzl/czejuw09tN4ZP6DN26+mMzt8yAkAXhAI61skcfiPi188PFnhIAX+BuLJAgEwxZ7BDvyqSGUEdgFUTjC0XiR2Ix39742hdqSPQ3sFoPw2kacZXGUh0YMGu3XL1LtjQ3d2BNcoHyZLjSGINuZpc0h6TbWe/jmHHcqnig+rZX8utNQBt/Mt18d0CrcJIwtQhIb9InWopuEtRlAx+0WKzNbOSbXdg5Ehg7XP9a6wPYFhJ51eSuVsbCfcWn9AYEUF/tppEPXkqxTTqjoNXYKVjEF0Cz6ICNYpZuPOQ4jyJB3kjUubD2a0AuvLAenzEXpmHasfrz0yKTHohLbjib5PFwdCqGEqJYJN+XyevQtqyZ52jQbhZdKITZm89niQ4ifiZ+fsDPDgIf88eJofRQ+rUOi+ELweJYQVCAavPfLz9CIh9yU2ZH1TmJIZ2qm0FowlNTfKZE= emendez@euclides",
    "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAICQnPlzDvQz1nXnCAz2G+pgX0+kCROmggSdz0uLCDXwY javier@pukara.es",
    "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIBYMV7qOpX6y8NmZYYG6wFHIPlLNQrT1eyMoez6Es/zI pablo@pukara.dev"
  ]
}

########
# Data #
########

data "onepassword_vault" "platform_automation" {
  name = "[Platform] Automation"
}

#######
# VPC #
#######

resource "scaleway_vpc" "pukara" {
  name = "pukara"
  tags = ["terraform", "shared_apps"]
}

resource "scaleway_vpc_private_network" "subnet_01" {
  name   = "main"
  tags   = ["kubernetes", "terraform"]
  vpc_id = scaleway_vpc.pukara.id

  ipv4_subnet {
    subnet = "192.168.0.0/20"
  }
}

#####################
# Database Instance #
#####################

# data "onepassword_item" "rdb" {
#   vault = data.onepassword_vault.platform_automation.uuid
#   uuid  = "Scaleway RDB - Pukara Instance Creds"
# }
#
# data "onepassword_item" "odoo_creds" {
#   vault = data.onepassword_vault.platform_automation.uuid
#   uuid  = "Scaleway RDB - Pukara Instance Creds"
# }
#
# resource "scaleway_rdb_instance" "pukara" {
#   name           = "pukara"
#   node_type      = "DB-DEV-S"
#   engine         = "PostgreSQL-15"
#   is_ha_cluster  = false
#   disable_backup = true
#   user_name      = data.onepassword_item.rdb.username
#   password       = data.onepassword_item.rdb.password
#
#   private_network {
#     pn_id       = scaleway_vpc_private_network.subnet_01.id
#     enable_ipam = false
#   }
# }
#
# resource "scaleway_rdb_database" "odoo" {
#   instance_id = scaleway_rdb_instance.pukara.id
#   name        = "odoo"
# }
#
# resource "scaleway_rdb_user" "odoo" {
#   instance_id = scaleway_rdb_instance.pukara.id
#   name        = "odoo"
#   password    = data.onepassword_item.odoo_creds.password
#   is_admin    = false
# }
#
# resource "scaleway_rdb_privilege" "odoo" {
#   instance_id   = scaleway_rdb_instance.pukara.id
#   user_name     = scaleway_rdb_user.odoo.name
#   database_name = scaleway_rdb_database.odoo.name
#   permission    = "all"
# }

#######
# K8S #
#######

resource "scaleway_k8s_cluster" "pukara" {
  name                        = "pukara"
  version                     = "1.31"
  cni                         = "cilium"
  private_network_id          = scaleway_vpc_private_network.subnet_01.id
  delete_additional_resources = false

  auto_upgrade {
    maintenance_window_start_hour = 20
    maintenance_window_day        = "any"
    enable                        = true
  }
}

resource "scaleway_k8s_pool" "pukara" {
  cluster_id = scaleway_k8s_cluster.pukara.id
  name       = "k8s"
  node_type  = "DEV1-M"
  size       = 2
}

resource "null_resource" "kubeconfig" {
  depends_on = [scaleway_k8s_pool.pukara]
  triggers = {
    host                   = scaleway_k8s_cluster.pukara.kubeconfig[0].host
    token                  = scaleway_k8s_cluster.pukara.kubeconfig[0].token
    cluster_ca_certificate = scaleway_k8s_cluster.pukara.kubeconfig[0].cluster_ca_certificate
  }
}

module "kubernetes_components" {
  # source = "git@github.com:pukara-dev/infrastructure.git//modules/kubernetes-components/?ref=a48e8bcc2470ddca175c1f45d6d0588b20365042"
  source = "../modules/kubernetes-components"
}