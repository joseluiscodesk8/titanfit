resource "scaleway_instance_ip" "public_ip" {
  count = var.public_ip ? 1 : 0

  type       = var.routed_ip_enabled ? "routed_ipv6" : "nat"
  project_id = var.project_id
}

resource "scaleway_instance_security_group" "default" {
  project_id              = var.project_id
  inbound_default_policy  = "drop"
  outbound_default_policy = "accept"

  dynamic "inbound_rule" {
    for_each = var.sg_ingress_ports

    content {
      action   = "accept"
      port     = inbound_rule.key
      ip_range = inbound_rule.value
    }
  }
}

resource "scaleway_instance_server" "server" {
  name              = var.instance_name
  project_id        = var.project_id
  type              = var.instance_type
  routed_ip_enabled = var.routed_ip_enabled
  image             = "ubuntu_jammy"
  state             = var.state

  tags = var.tags

  ip_id = var.public_ip ? scaleway_instance_ip.public_ip[0].id : null

  security_group_id = scaleway_instance_security_group.default.id
  cloud_init = templatefile("${path.module}/templates/cloud-init.tpl", {
    ssh_keys = var.ssh_keys,
    packages = var.packages
  })
}
